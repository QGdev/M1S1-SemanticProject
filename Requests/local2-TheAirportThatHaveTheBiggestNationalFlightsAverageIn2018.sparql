#	Requête faite par Quentin GOMES DOS REIS

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX ex: <http://example.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dbo: <http://dbpedia.org/ontology/>

# Affichage des données intéressantes ainsi que le rapport entre le nb de passagers transportés et le nombre de vols
SELECT  ?countryCode ?icao ?nbOfFlights ?transportedPass (xsd:float(xsd:integer(?transportedPass)/xsd:integer(?nbOfFlights)) AS ?passengersPerFlight)

WHERE {
  # Récupération le nombre de vols commerciaux de passagers
  ?x	time:year 2018;
    	ex:declarativeAirport ?airport;
    	ex:measuredMetric "CAF_PAS";
    	ex:coverageTransport "NAT";
     	ex:airline "TOTAL";
    	rdf:value ?nbOfFlights.
  
  # Récupération le nombre de passagers transportées
  ?y	time:year 2018;
    	ex:declarativeAirport ?airport;
    	ex:measuredMetric "PAS_CRD";
    	ex:coverageTransport "NAT";
        ex:airline "TOTAL";
    	rdf:value ?transportedPass.
  
  # Récupération des détails de l'aéroport
  ?airport	dbo:iso31661Code ?countryCode;
      		dbo:icaoLocationIdentifier ?icao.

  # On évite la division par zéro impossible      
  FILTER( ?nbOfFlights > 0)
}
ORDER BY DESC(?passengersPerFlight)
LIMIT 30
